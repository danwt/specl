module EWD840

// Dijkstra's termination detection on a ring (EWD840)
// N is the last node index, so 0..N gives N+1 nodes.
// Use: -c N=4

const N: Int

var active: Dict[Int, Bool]    // node activation status
var color: Dict[Int, String]   // node color ("white" or "black")
var tpos: Int                  // token position
var tcolor: String             // token color

init {
    active = {i: true for i in 0..N};
    color = {i: "white" for i in 0..N};
    tpos = 0;
    tcolor = "black";
}

invariant TerminationDetection {
    (tpos == 0 and tcolor == "white" and color[0] == "white" and not active[0])
    implies (all i in 0..N: not active[i])
}

action InitiateProbe() {
    require tpos == 0;
    require tcolor == "black" or color[0] == "black";
    tpos = N;
    tcolor = "white";
    color = color | { 0: "white" };
}

action PassToken(i: 0..N) {
    require i > 0;
    require tpos == i;
    require not active[i] or color[i] == "black" or tcolor == "black";
    tpos = i - 1;
    tcolor = (if color[i] == "black" then "black" else tcolor);
    color = color | { i: "white" };
}

action SendMsg(i: 0..N, j: 0..N) {
    require active[i];
    require i != j;
    active = active | { j: true };
    color = color | { i: (if j > i then "black" else color[i]) };
}

action Deactivate(i: 0..N) {
    require active[i];
    active = active | { i: false };
}

action Stutter() {
    require tpos == 0 and tcolor == "white" and color[0] == "white" and not active[0];
}
