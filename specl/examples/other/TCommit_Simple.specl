// Use: No constants needed
module TCommit

var rmState: Dict[Int, String]

init {
    rmState = {rm: "working" for rm in 1..5};
}

invariant TypeOK {
    all k in 1..5: (rmState[k] in {"working", "prepared", "committed", "aborted"})
}

invariant Consistent {
    all rm1 in 1..5: all rm2 in 1..5: not (rmState[rm1] == "aborted" and rmState[rm2] == "committed")
}

action Prepare(rm: 1..5) {
    require rmState[rm] == "working";
    rmState = rmState | { rm: "prepared" };
}

action DecideCommit(rm: 1..5) {
    require rmState[rm] == "prepared";
    require all r in 1..5: (rmState[r] in {"prepared", "committed"});
    rmState = rmState | { rm: "committed" };
}

action DecideAbort(rm: 1..5) {
    require rmState[rm] in {"working", "prepared"};
    require all r in 1..5: rmState[r] != "committed";
    rmState = rmState | { rm: "aborted" };
}

action Stutter() {
    require all rm in 1..5: (rmState[rm] in {"committed", "aborted"});
}
